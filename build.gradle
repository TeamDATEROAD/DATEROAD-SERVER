plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'

}

bootJar.enabled = false // 빌드시 현재 모듈(multi-module)의 .jar를 생성하지 않습니다.

repositories {
	mavenCentral()
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.dateroad'
	version = '0.0.1-SNAPSHOT'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
		// test db
		runtimeOnly 'com.h2database:h2'
		
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

project(':dateroad-external') {
	dependencies {
		implementation project(':dateroad-common')
	}
}

project(':dateroad-domain') {
	dependencies {
		implementation project(':dateroad-common')
	}
}

project(':dateroad-api') {
	dependencies {
		implementation project(':dateroad-external')
		implementation project(':dateroad-common')
		implementation project(':dateroad-domain')
	}
}